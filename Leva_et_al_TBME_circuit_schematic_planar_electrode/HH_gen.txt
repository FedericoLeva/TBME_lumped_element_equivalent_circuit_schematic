%HH subcircuit at T=6.3°C
.subckt HH_gen extra intra params: Area={Ai(1)} muNa={uNa} muK={uK} kl=1
Rl intra N003 {3333*kl/Area}
Cm intra extra {Area*1u}
VÂ§Ena N001 extra 50m
VÂ§Ek N002 extra -77m
VÂ§El N003 extra -54.4m
XU2 N001 intra Na_Memristor_HH2 Ena=115m A=Area u=muNa
XU1 N002 intra K_Memristor_HH2 Ek=12m A=Area u=muK
.ends HH_gen


.subckt Na_Memristor_HH2 in1 in2 params: Ena=115m A=1 u=1
.param gnmax=120m
.param gna={gnmax*A*u}
.param minit 0
.param hinit 0
.funct Gmh(m,h)={gna*m**3*h}
.funct alpham(v)={2500*(40*(v-Ena)+ 1)/( exp(100*(v-Ena)+ 2.5 )-1)}
.funct betam(v)={4000*exp((v-Ena)/0.018)}
.funct alphah(v)={70*exp(50*(v-Ena))}
.funct betah(v)={1000/(exp(100*(v-Ena)+3)+1)}
.funct fm(m,v)={alpham(v)*(1-m)-betam(v)*m}
.funct fh(h,v)={alphah(v)*(1-h)-betah(v)*h}
G in1 in2 value={v(in1,in2)*Gmh(v(m),v(h))}
Emd md 0 value={fm(v(m),v(in1,in2))}
Ehd hd 0 value={fh(v(h),v(in1,in2))}
Em m 0 value={minit+IDT(v(md))}
Eh h 0 value={hinit+IDT(v(hd))}
.ends

.subckt K_Memristor_HH2 in1 in2 params: Ek=12m A=1 u=1
.param gkmax 36m
.param gk={gkmax*A*u}
.param ninit 0
.funct Gn(n)={gk*n**4}
.funct alphan(v)={100*(100*(v+Ek)+ 1)/( exp(100*(v+Ek)+ 1 )-1)}
.funct betan(v)={125*exp(12.5*(v+Ek))}
.funct fn(n,v)={alphan(v)*(1-n)-betan(v)*n}
G in1 in2 value={v(in1,in2)*Gn(v(n))}
Ed nd 0 value={fn(v(n),v(in1,in2))}
E n 0 value={ninit+IDT(v(nd))}
.ends
